// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© tradedots

//@version=5
strategy("Chande Momentum Oscillator (CMO) Buy Sell Strategy [TradeDots]", overlay=false, max_lines_count = 500, max_boxes_count = 100, initial_capital = 10000, currency = currency.USD, commission_type = strategy.commission.percent, commission_value = 0.01, default_qty_type = strategy.percent_of_equity, default_qty_value = 80)

// user input
enable_backtest     = input.bool(defval = true, title = "Enable Backtest")
source              = input(close, "Source", group = "Chande Momentum Oscillator")
length              = input(9, "CMO Length", group = "Chande Momentum Oscillator")
order_direction     = input.string("Long & Short", "Order Direction", ["Long & Short", "Long Only", "Short Only"], group = "Strategy")
cool_down_period    = input.int(defval = 5, title = "Signal Cool Down Period", minval = 1, step = 1, group = "Strategy")
overbought_tresh    = input(40, "Selling Threshold", group = "Strategy")
oversold_tresh      = input(-40, "Buying Threshold", group = "Strategy")

// var
var int buy_counter  = cool_down_period
var int sell_counter = cool_down_period

// cmo
cmo                 = ta.cmo(source, length)

// EMA
signal              = ta.ema(cmo, 9)

// hist
hist                = cmo - signal

//hist coloring
histA_IsUp          = hist > hist[1] and hist > 0
histA_IsDown        = hist < hist[1] and hist > 0
histB_IsDown        = hist < hist[1] and hist <= 0
histB_IsUp          = hist > hist[1] and hist <= 0
hist_color          = histA_IsUp ? #38a3a5 : histA_IsDown ? #cbf3f0 : histB_IsDown ? #0096c7 : histB_IsUp ? #ade8f4 : color.gray

// plotting
plot(cmo, "CMO Line", color = #9cff87)
plot(signal, "CMO Signal Line", color = #f9396a)
plot(hist, "CMO Hist", color = hist_color, style=plot.style_columns,  linewidth = 5)

plot(overbought_tresh, "Selling Threshold", color = color.from_gradient(cmo, 0, overbought_tresh, color.new(color.gray, 70), color.new(#f9396a, 0)), linewidth = 2)
plot(oversold_tresh, "Buying Threshold", color = color.from_gradient(cmo, oversold_tresh, 0, color.new(#9cff87, 0), color.new(color.gray, 70)), linewidth = 2)

// reference line
hline(overbought_tresh, title = "Overbought Threshold")
hline(0, title = "0 Line")
hline(oversold_tresh, title = "Oversold Threshold")

// Strategy entry condition
if signal > overbought_tresh and histA_IsDown and order_direction != "Long Only"
    if sell_counter >= cool_down_period
        label.new(bar_index, signal, text = "ðŸ”´", style = label.style_label_down, color = #f9396a, textcolor = #ffffff)
        if enable_backtest
            strategy.entry("Sell", strategy.short, comment = "Open Short Position")

        sell_counter := 0
        buy_counter := cool_down_period
    else
        sell_counter := sell_counter + 1

else if signal < oversold_tresh and histB_IsUp and order_direction != "Short Only"
    if buy_counter >= cool_down_period
        label.new(bar_index, signal, text = "ðŸŸ¢", style = label.style_label_up, color = #9cff87)
        if enable_backtest
            strategy.entry("Buy", strategy.long, comment = "Open Long Position")
        buy_counter := 0
        sell_counter := cool_down_period
    else
        buy_counter := buy_counter + 1

// Stategy exit
if enable_backtest
    if signal > overbought_tresh and strategy.opentrades.size(strategy.opentrades - 1) > 0
        strategy.close("Buy", qty_percent = 100, comment = "Close Long Position")
        label.new(bar_index, signal, text = "Close", style = label.style_label_down, color = #f9396a, textcolor = #ffffff)
        buy_counter := cool_down_period

    else if signal < oversold_tresh and strategy.opentrades.size(strategy.opentrades - 1) < 0
        strategy.close("Sell", qty_percent = 100, comment = "Close Short Position")
        label.new(bar_index, signal, text = "Close", style = label.style_label_up, color = #9cff87)
        sell_counter := cool_down_period

//reset cool down period after signal line back to normal state
if signal < overbought_tresh
    sell_counter := cool_down_period

if signal > oversold_tresh
    buy_counter := cool_down_period