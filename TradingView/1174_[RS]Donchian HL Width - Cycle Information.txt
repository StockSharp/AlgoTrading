// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© RicardoSantos

//@version=4
study(title='[RS]Donchian HL Width - Cycle Information')

f_ph(_length) =>
    float _max = highest(_length)
    float _min = lowest(_length)
    100 * (high - _max) / (_max - _min)
f_pl(_length) =>
    float _max = highest(_length)
    float _min = lowest(_length)
    100 * (low - _max) / (_max - _min)

int length = input(28)
bool show_barcolor = input(true)

float ph = f_ph(length)
float pl = f_pl(length)
float avg = avg(ph, pl)
float mean = cum(avg) / (bar_index + 1)

var int cycle_counter = 0
cycle_counter := nz(cycle_counter) + 1
var int cycle_trend = 1

var int cycle_count = 0
var int[] cycles = array.new_int(0)

if cycle_trend < 0 and ph >= 0.0
    label.new(bar_index, 0.0, '', color=color.green, style=label.style_arrowdown)
    array.push(cycles, cycle_counter)
    cycle_counter := 0
    cycle_trend := +1
    cycle_count += 1
if cycle_trend > 0 and pl <= -100.0
    label.new(bar_index, -100.0, '', color=color.maroon, style=label.style_arrowup)
    array.push(cycles, cycle_counter)
    cycle_counter := 0
    cycle_trend := -1
    cycle_count += 1

plot(series=array.avg(cycles), title='Cycle AVG', color=color.orange)
plot(series=cycle_counter, title='Cycle Counter', color=cycle_trend>0?color.lime:color.red)
plot(series=ph, title='%Wd high')
plot(series=pl, title='%Wd low')
plot(series=avg, title='HL AVG', color=color.black)
plot(series=mean, title='Mean', color=color.navy)

color col = na
if show_barcolor
    if avg > -40
        col := color.green
    else if avg < -60
        col := color.maroon
    else
        col := color.silver
barcolor(col)