// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © PresentTrading

// This strategy stands out by using two distinct RSI lengths, analyzing the differential between these to make precise trading decisions.
// Unlike conventional single RSI strategies, this method provides a more nuanced view of market dynamics, allowing traders to exploit
// both overbought and oversold conditions with greater accuracy.

//@version=5
strategy("Dual RSI Differential - Strategy [presentTrading]", overlay=false, precision=3,
 commission_value=0.1, commission_type=strategy.commission.percent, slippage=1,
 currency=currency.USD, default_qty_type=strategy.percent_of_equity, default_qty_value=10, initial_capital=10000)

// Input parameters for user customization
tradeDirection = input.string("Both", "Trading Direction", options=["Long", "Short", "Both"])
lengthShort = input(21, title="Short RSI Period")
lengthLong = input(42, title="Long RSI Period")
rsiDiffLevel = input(5, title="RSI Difference Level")
useHoldDays = input.bool(true, title="Use Hold Days")
holdDays = input.int(5, title="Hold Days", minval=1, maxval=20, step=1)
TPSLCondition = input.string("None", "TPSL Condition", options=["TP", "SL", "Both", "None"])

takeProfitPerc = input(15.0, title="Take Profit (%)")
stopLossPerc = input(10.0, title="Stop Loss (%)")
// Calculate RSIs
rsiShort = ta.rsi(close, lengthShort)
rsiLong = ta.rsi(close, lengthLong)

// Calculate RSI Difference
rsiDifference = rsiLong  - rsiShort

// Plotting
hline(rsiDiffLevel, "Level +20", color=color.green, linestyle=hline.style_dashed)
hline(-rsiDiffLevel, "Level -20", color=color.red, linestyle=hline.style_dashed)

// Variables to track entry times
var float longEntryTime = na
var float shortEntryTime = na

// Condition for significant RSI difference
combinedLongCondition = rsiDifference < -rsiDiffLevel
combinedExitLongCondition = rsiDifference > rsiDiffLevel
combinedShortCondition = rsiDifference > rsiDiffLevel
combinedExitShortCondition = rsiDifference < -rsiDiffLevel

// Strategy logic using conditions and direction selection
if (tradeDirection == "Long" or tradeDirection == "Both")
    if (combinedLongCondition)
        strategy.entry("Long", strategy.long)
        longEntryTime := time
    if (useHoldDays and (time - longEntryTime >= holdDays * 86400000 or combinedExitLongCondition))
        strategy.close("Long")
    else if (useHoldDays == false  and combinedExitLongCondition)
        strategy.close("Long")

if (tradeDirection == "Short" or tradeDirection == "Both")
    if (combinedShortCondition)
        strategy.entry("Short", strategy.short)
        shortEntryTime := time
    if (useHoldDays and (time - shortEntryTime >= holdDays * 86400000 or combinedExitShortCondition))
        strategy.close("Short")
    else if (useHoldDays == false and combinedExitShortCondition)
        strategy.close("Short")


// Conditional Profit and Loss Management
if (TPSLCondition == "TP" or TPSLCondition == "Both")
    // Apply take profit conditions
    strategy.exit("TakeProfit_Long", "Long", profit=close * (1 + takeProfitPerc / 100), limit=close * (1 + takeProfitPerc / 100))
    strategy.exit("TakeProfit_Short", "Short", profit=close * (1 - takeProfitPerc / 100), limit=close * (1 - takeProfitPerc / 100))


if (TPSLCondition == "SL" or TPSLCondition == "Both")
    // Apply stop loss conditions
    strategy.exit("StopLoss_Long", "Long", loss=close * (1 - stopLossPerc / 100), stop=close * (1 - stopLossPerc / 100))
    strategy.exit("StopLoss_Short", "Short", loss=close * (1 + stopLossPerc / 100), stop=close * (1 + stopLossPerc / 100))


bgcolor(combinedLongCondition ? color.new(color.green, 90) : na, title="Background Color for Significant Long RSI Diff")
bgcolor(combinedShortCondition ? color.new(color.red, 90) : na, title="Background Color for Significant Short RSI Diff")

// Plot RSIs and their difference
plot(rsiDifference, title="RSI Difference (35-7)", color=color.fuchsia)

// Alerts
alertcondition(combinedLongCondition, title="Significant Long RSI Difference Alert", message="RSI Difference is significant Long at {{close}} with RSI7 at {{rsiShort}} and RSI35 at {{rsiLong}}.")
alertcondition(combinedShortCondition, title="Significant Short RSI Difference Alert", message="RSI Difference is significant Short at {{close}} with RSI7 at {{rsiShort}} and RSI35 at {{rsiLong}}.")