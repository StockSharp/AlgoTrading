// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © PakkunFX

//@version=5
strategy("FVG Positioning Average with 200EMA Auto Trading [Pakun]", overlay=true)

//---------------------------------------------------------------------------------------------------------------------
// User Inputs
//---------------------------------------------------------------------------------------------------------------------
lb = input.int(30, title = "FVG Lookback", tooltip = "Determines how many FVGs to consider for calculating the averages.", group = "Settings")
lbType = input.string("Bar Count", title = "Lookback Type", options = ["Bar Count","FVG Count"], tooltip = "Bar Count => Uses any FVGs within this Bar Lookback\nFVG Count => Uses only this amount of recent FVGs", group = "Settings")
atrMulti = input.float(0.25, step = 0.25, title = "ATR Multiplier", tooltip = "Only uses FVGs that are greater than ATR * Multiplier.", group = "Settings")
lookbackPeriod = input.int(20, title="Lookback Period", tooltip="The number of bars to look back for the recent low", group = "Settings")
emaPeriod = input.int(200, title="EMA Period", tooltip="The period for the EMA", group = "Settings")

fvgTog = input.bool(false, title = "Show FVGs on Chart", group = "Style")
green =  input.color(#089981, title = "Bullish Color", group = "Style")
red = input.color(#f23645, title = "Bearish Color", group = "Style")
showFill = input.bool(false, title = "Show Gradient Areas", group = "Style")

invis = color.rgb(0,0,0,100)

//---------------------------------------------------------------------------------------------------------------------
// Constants
//---------------------------------------------------------------------------------------------------------------------
atr = nz(ta.atr(200)*atrMulti, ta.cum(high - low) / (bar_index+1))

// FVG Detection
fvg_up = low > high[2] and close[1] > high[2] and (low-high[2]) > atr
fvg_down = high < low[2] and close[1] < low[2] and (low[2]-high) > atr

// Highest/Lowest
hst = ta.highest(high,5)
lst = ta.lowest(low,5)

//---------------------------------------------------------------------------------------------------------------------
// Calculations
//---------------------------------------------------------------------------------------------------------------------
var up_ary = array.new_box(na)
var down_ary = array.new_box(na)

if fvg_up
    up_ary.push(
      box.new(bar_index[2], low, bar_index, high[2]
      , border_color = fvgTog?color.new(green,50):na
      , bgcolor = fvgTog?color.new(green,50):na))

if fvg_down
    down_ary.push(
      box.new(bar_index[2],  low[2], bar_index, high
      , border_color = fvgTog?color.new(red,50):na
      , bgcolor = fvgTog?color.new(red,50):na))

// Array Size Management
if lbType == "FVG Count"
    if up_ary.size() > lb
        box.delete(up_ary.shift())
    if down_ary.size() > lb
        box.delete(down_ary.shift())

if lbType == "Bar Count"
    for [index,bx] in up_ary
        left = bx.get_left()
        if left < bar_index-lb
            bx.delete()
            up_ary.remove(index)

    for [index,bx] in down_ary
        left = bx.get_left()
        if left < bar_index-lb
            bx.delete()
            down_ary.remove(index)

// Value Accumulation
up_vals = array.new_float(na)
down_vals = array.new_float(na)

for bx in up_ary
    up_vals.push(bx.get_bottom())
for bx in down_ary
    down_vals.push(bx.get_top())

// Averages
up_avg = array.avg(up_vals)
down_avg = array.avg(down_vals)

// Points to use for gradients
c_mid_h = math.max(math.avg(open,close),up_avg)
c_mid_l = math.min(math.avg(open,close),down_avg)

//---------------------------------------------------------------------------------------------------------------------
// Display
//---------------------------------------------------------------------------------------------------------------------
ua = plot(up_avg
  , color = (hst<up_avg?invis:green)
  , title = "Bull Average"
  , display = display.none)  // Default to not displayed

da = plot(down_avg
  , color = (lst>down_avg?invis:red)
  , title = "Bear Average"
  , display = display.none)  // Default to not displayed

ul = plot(c_mid_h, display = display.none, editable = false)
dl = plot(c_mid_l, display = display.none, editable = false)

fill(ua, ul
  , c_mid_h
  , math.min(ta.sma(up_avg,10),up_avg)
  , color.new(chart.bg_color,100)
  , c_mid_h<=up_avg?invis:color.new(green,50)
  , display = showFill ? display.all : display.none)

fill(da, dl
  , math.max(ta.sma(down_avg,10),down_avg)
  , c_mid_l
  , c_mid_l>=down_avg?invis:color.new(red,50)
  , color.new(chart.bg_color,100)
  , display = showFill ? display.all : display.none)

//---------------------------------------------------------------------------------------------------------------------
// EMA Calculation
//---------------------------------------------------------------------------------------------------------------------
ema = ta.ema(close, emaPeriod)

//---------------------------------------------------------------------------------------------------------------------
// Detect Recent Lows and Highs
//---------------------------------------------------------------------------------------------------------------------
recentLow = ta.lowest(low, lookbackPeriod)
recentHigh = ta.highest(high, lookbackPeriod)

//---------------------------------------------------------------------------------------------------------------------
// Trading Conditions
//---------------------------------------------------------------------------------------------------------------------
longCondition = ta.crossover(close, down_avg) and close > ema and up_avg > ema and down_avg > ema
shortCondition = ta.crossunder(close, up_avg) and close < ema and up_avg < ema and down_avg < ema

// Risk-Reward Setup
riskRewardRatio = 1.5

// Long Trade Setup
if (longCondition)
    longStopLoss = recentLow
    longTakeProfit = close + (close - longStopLoss) * riskRewardRatio
    strategy.entry("Long", strategy.long)
    strategy.exit("Take Profit/Stop Loss", "Long", stop=longStopLoss, limit=longTakeProfit)

// Short Trade Setup
if (shortCondition)
    shortStopLoss = recentHigh
    shortTakeProfit = close - (shortStopLoss - close) * riskRewardRatio
    strategy.entry("Short", strategy.short)
    strategy.exit("Take Profit/Stop Loss", "Short", stop=shortStopLoss, limit=shortTakeProfit)