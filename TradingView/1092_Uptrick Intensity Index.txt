// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © MicGra

//@version=5
strategy("Uptrick:Intensity Index", overlay=false)

// Input parameters
length1 = input(10, title="MA1 Length")
length2 = input(20, title="MA2 Length")
length3 = input(50, title="MA3 Length")
tii_sma_length = input(50, title="Length of SMA of TII")

// New input parameters for customization
tii_color = input.color(color.blue, title="TII Plot Color")
tii_ma_color = input.color(color.orange, title="TII MA Plot Color")
bg_color_positive = input.color(color.green, title="Background Color (Positive TII MA)")
bg_color_negative = input.color(color.red, title="Background Color (Negative TII MA)")
bg_transparency = input.int(90, title="Background Transparency", minval=0, maxval=100)

// Calculate moving averages
ma1 = ta.sma(close, length1)
ma2 = ta.sma(close, length2)
ma3 = ta.sma(close, length3)

// Calculate the relative positions
rel1 = (close - ma1) / ma1
rel2 = (close - ma2) / ma2
rel3 = (close - ma3) / ma3

// Calculate the Trend Intensity Index
tii = (rel1 + rel2 + rel3) / 3 * 100

matii = ta.sma(tii, tii_sma_length)

// Plot the TII and its SMA
hline(0, "Zero Line", color=color.gray)
plot(tii, color=tii_color, title="Trend Intensity Index")
plot(matii, color=tii_ma_color, title='TII MA')

// Background color based on matii value
bgcolor(matii > 0 ? color.new(bg_color_positive, bg_transparency) : color.new(bg_color_negative, bg_transparency))

// Strategy conditions
if ta.crossover(tii, matii)
    strategy.entry('Buy', strategy.long)
if ta.crossunder(tii, matii)
    strategy.entry('Sell', strategy.short)