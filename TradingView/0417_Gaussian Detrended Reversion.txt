// Â© DraftVenture

//@version=5
strategy(title="Gaussian Detrended Price Reversion Strategy", shorttitle = "GDR", overlay=false)

// Detrended Price Oscillator base inputs and calculations
period_ = input.int(52, title="Price Length", minval=1)

barsback = period_/2 + 1
ma = ta.ema(close, period_)
dpo = close - ma[barsback]

// ALMA smoothing and inputs on the DPO
almaSource = dpo
almaWindowSize = input(title="Smoothing Length", defval=52)
lagLength = input(title="Lag Length", defval=26)
almaDPO = ta.alma(almaSource, almaWindowSize, 0.85, 6)
almaLag = almaDPO[lagLength]

// Conditions
entryL = ta.crossover(almaDPO, almaLag) and almaDPO < 0
exitL = ta.crossunder(almaDPO, almaLag) or ta.crossunder(almaDPO, 0)
entryS = ta.crossunder(almaDPO, almaLag) and almaDPO > 0
exitS = ta.crossover(almaDPO, almaLag) or ta.crossover(almaDPO, 0)

// Logic
if entryL
    strategy.entry("Long", strategy.long)

if exitL
    strategy.close("Long")

if entryS
    strategy.entry("Short", strategy.short)

if exitS
    strategy.close("Short")

// Plotting
plot(almaDPO, title="Price Line", color=color.green)
plot(almaDPO, title="Lag Line", offset = lagLength, color=color.white)

hline(0, title="Zero Rate Line", color=color.white)

bgcolor(entryL ? color.new(color.green, 40) : na, title = "Long Entry Highlighter")
bgcolor(entryS ? color.new(color.red, 40) : na, title = "Short Entry Highlighter")

plotshape(series=exitL, style=shape.xcross, location=location.top, color=color.green, size=size.tiny, title = "Long Exit Symbol")
plotshape(series=exitS, style=shape.xcross, location=location.bottom, color=color.red, size=size.tiny, title = "Short Exit Symbol")

//______ _________
//___  //_/__  __ \
//__  ,<  __  /_/ /
//_  /| | _  ____/
///_/ |_| /_/