//@version=6
strategy("[SHORT ONLY] Consecutive Close>High[1] Mean Reversion Strategy", overlay = false, precision = 4, initial_capital = 1000000, default_qty_value = 100, default_qty_type = strategy.percent_of_equity, process_orders_on_close = true, margin_long = 5, margin_short = 5, calc_on_every_tick = true)

//#region INPUTS SECTION
// ============================================
// Time Settings
// ============================================
startTimeInput = input.time(timestamp("1 Jan 2014"), "Start Time", group = "Time Settings")
endTimeInput = input.time(timestamp("1 Jan 2099"), "End Time", group = "Time Settings")
isWithinTradingWindow = time >= startTimeInput and time <= endTimeInput

// ============================================
// Strategy Settings
// ============================================
lookback = input.int(defval = 3, minval = 1, title = 'Threshold', group = "Strategy Signal")

// ============================================
// EMA Filter Settings
// ============================================
useEmaFilter = input.bool(true, "Use EMA Filter", group = "Trend Filter")
emaPeriodInput = input.int(200, "EMA Period", minval = 1, group = "Trend Filter")
//#endregion

//#region INDICATOR CALCULATIONS
// ============================================
// Consecutive Close>High[1] Counter
// ============================================
var int bullCount = 0
if close > high[1]
    bullCount += 1
if close < low[1]
    bullCount := 0

plot(bullCount, title="Bullish Count", color=color.green, style=plot.style_columns)
hline(lookback, title="Signal Threshold")

// ============================================
// Trend Filter
// ============================================
ma200 = ta.ema(close, emaPeriodInput)
plot(ma200, color=color.red, force_overlay=true)

//#region TRADING CONDITIONS
// ============================================
// Entry/Exit Logic
// ============================================
shortCondition = bullCount >= lookback and isWithinTradingWindow
exitCondition = close<low[1]

// Apply EMA Filter if enabled
if useEmaFilter
    shortCondition := shortCondition and close < ma200
//#endregion

//#region STRATEGY EXECUTION
// ============================================
// Order Management
// ============================================
if shortCondition
    strategy.entry('short', strategy.short)
if exitCondition
    strategy.close_all()
//#endregion