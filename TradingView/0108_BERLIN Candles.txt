// ------------------------------------------------------------------------------------
//
//    BERLIN Candles
//    Version 1.0
//
// ------------------------------------------------------------------------------------
//
// A problem with Heikin Ashi is that while it gives you a great overview of overall
// direction, it is rarely possible to use it as a replacement for normal japanese
// candlesticks. The reason for this is that actual price data is lost, since the
// candles are more akin to a moving average than a different way to see price action.
// Also, with Heikin Ashi, most of the actual price action is lost, because the candles
// can be bigger than the high and low of the underlying japanese candlestick.
//
// With BERLIN Candles I have tried to fix that problem. By using a smoothed out
// version of the previous Heikin Ashi candle close as the current BERLIN Candle open,
// the high and low of the actual japanese candlestick for the high and low of the
// BERLIN Candle, and the current Heikin Ashi close as the BERLIN Candle close, while
// setting hard limits for BERLIN Candle open and close values so that they can never
// exceed the high and low of the underlying japanese candlestick.
//
// One problem still persists though. The actual current price data is lost. However,
// the BERLIN Candles have solved this by adding a fifth part to the candles. The close
// of the underlying japanese candlesticks are indicated with a plus-sign. This way,
// actual price data is never lost, while keeping all of the other benefits of this new
// type of candle.
//
// A few added bonuses:
//    * The addition of the 14 period ATR at the latest candle
//    * The baseline from Ichimoku is included as an option
//    * The 14 period ATR value of each candle can be seen in the indicator data as
//      the orange value
//
// ------------------------------------------------------------------------------------
//
// Licensed under CC BY-NC.
// https://creativecommons.org/licenses/by-nc/4.0/
//
// Copyright Â© Anton "lejmer" Berlin, 2020.
//
// ------------------------------------------------------------------------------------

//@version=4

study("BERLIN Candles", overlay = true)

// ------------------------------------------------------------------------------------
//    Input
// ------------------------------------------------------------------------------------

smoothing = input(defval = 1, minval = 1, maxval = 50, type = input.integer)
use_bg_color = input(title = "Use candle color as background color?", defval = false, type = input.bool)
use_bars = input(title = "Plot bars instead of candles?", defval = false, type = input.bool)
show_close = input(title = "Show true close prices?", defval = true, type = input.bool)
show_atr = input(title = "Show ATR value at latest bar?", defval = true, type = input.bool)
show_baseline = input(title = "Show baseline?", defval = false, type = input.bool)

// ------------------------------------------------------------------------------------
//    User-defined functions
// ------------------------------------------------------------------------------------

donchian(src, len) =>
    avg(lowest(src, len), highest(src, len))

// ------------------------------------------------------------------------------------
//    Calculations
// ------------------------------------------------------------------------------------

atr_val = atr(14)

ha_symbol = heikinashi(syminfo.ticker)
open_expr = ema(close[1], smoothing + 1)
close_expr = close

real_close = security(syminfo.ticker, timeframe.period, close)

h = security(syminfo.ticker, timeframe.period, high)
l = security(syminfo.ticker, timeframe.period, low)
o = security(ha_symbol, timeframe.period, open_expr > close_expr ? min(open_expr, h) : max(open_expr, l))
c = security(ha_symbol, timeframe.period, open_expr > close_expr ? max(close_expr, l) : min(close_expr, h))

baseline = donchian(c, 26)

// ------------------------------------------------------------------------------------
//    Colors
// ------------------------------------------------------------------------------------

clr = o < c ? color.green : color.red

// ------------------------------------------------------------------------------------
//    Plotting
// ------------------------------------------------------------------------------------

plotbar(not use_bars ? na : o, not use_bars ? na : h, not use_bars ? na : l, not use_bars ? na : c, title = "Bars", color = clr)
plotcandle(use_bars ? na : o, use_bars ? na : h, use_bars ? na : l, use_bars ? na : c, title = "Candles", color = clr)
plotshape(title = "Close prices", series = real_close, color = show_close ? color.gray : na, transp = 0, size = size.tiny, style = shape.cross, location = location.absolute)
plot(title = "Baseline", series = show_baseline ? baseline : na, color = color.orange)

bgcolor(color = use_bg_color ? clr : na, transp = 90)

plotshape(atr_val, color = color.orange, transp = 100)

lbl = label.new(x = bar_index, y = na,
     text = "ATR: " + tostring(atr_val, "#.####"), color = show_atr ? color.orange : na, textcolor = show_atr ? color.black : na,
     style = label.style_labeldown, yloc = yloc.abovebar)

label.delete(lbl[1])

// ------------------------------------------------------------------------------------
//    Output for indicators that have a source option
// ------------------------------------------------------------------------------------

plot(title = "Candle open (use as source for other indicators)", series = o, color = na)
plot(title = "Candle high (use as source for other indicators)", series = h, color = na)
plot(title = "Candle low (use as source for other indicators)", series = l, color = na)
plot(title = "Candle close (use as source for other indicators)", series = c, color = na)