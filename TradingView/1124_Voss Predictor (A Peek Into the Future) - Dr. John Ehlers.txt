//@version=4
study("Voss Predictor (A Peek Into the Future) - Dr. John Ehlers", "VPF", false, format.price, 3)

bgcolor(color.new(#000000,20), title="Dark Background")
var color_none = color(na)

whiten(Series) =>
    0.5 * (Series - nz(Series[2], nz(Series[1], Series)))

bpf(Series, Period, Bandwidth) => // Ehler's BandPass Filter
    var PIx2  = 4.0 * asin(1.0) // 6.28318530718 Constant
    var alpha = PIx2 / Period
    var gamma = cos(alpha * Bandwidth)
    var delta = 1.0 / gamma - sqrt(1.0 / pow(gamma, 2.0) - 1.0)
    float bandPass = na,  bandPass := (1.0 - delta) * whiten(  Series   ) +
                         cos(alpha) * (1.0 + delta) *     nz(bandPass[1]) -
                                             delta  *     nz(bandPass[2])
    bandPass

vpf(Series, BarsOfPrediction) => // Ehler's Voss Predictive Filter
    var order  = 3.0 * min(3.0, BarsOfPrediction)
    float voss = na
    E = 0.0, for i=0 to int(order-1)
        E := nz(voss[order - i]) * (  1 + i    ) / order  + E
    voss  :=                 0.5 * (3.0 + order) * Series - E
    voss

source         = input(         close,                             "Source", input.source )
showAreaBP     = input(          true,              "Display Bandpass Area", input.bool   )
periodBandpass = input(            20,                    "Bandpass Period", input.integer,  minval=   2)
bandWidth      = input(          0.25,                        "  Bandwidth", input.float  ,  minval=0.05, maxval=1.0, step=0.05)
barsPrediction = input(           3.0,                 "Bars of Prediction", input.float  ,  minval=0.5 , maxval=3.0, step=0.5 )
showCorrColor  = input(          true, "===== Show Correlation Color =====", input.bool   )
syncCorrPeriod = input("Synchronized",               " Correlation Control", input.string , options=["Independent", "Synchronized"])
periodCorr     = input(            40,                " Correlation Period", input.integer,  minval=2)
var periodCorrelation = syncCorrPeriod=="Synchronized" ? periodBandpass : periodCorr

BPF = bpf(  source, periodBandpass, bandWidth)
VPF = vpf(     BPF, barsPrediction)

colorFill =  BPF>0.0 and VPF>BPF ? #00FF00 :
             BPF<0.0 and VPF<BPF ? #FF0000 : color_none
plot(showAreaBP ? BPF : na, "Area", color=#AAFFFF80, style=plot.style_area)
plotVPF = plot(   VPF,       "VPF", color= VPF>0.0 ? #00FF00 : #FF0000)
plotBPF = plot(   BPF,       "BPF", color=#EEEEEEff, linewidth= 2)
fill(plotBPF, plotVPF,   colorFill, 75, "", editable=false)

//===== Correlation Color
correlate = correlation(source, bar_index, periodCorrelation)
colorCorrelate = correlate> 0.75 ? #00FF00ff :
                 correlate> 0.50 ? #00C000ff :
                 correlate> 0.25 ? #008000ff :
                 correlate> 0.0  ? #004000ff :
                 correlate>-0.25 ? #400000ff :
                 correlate>-0.50 ? #800000ff :
                 correlate>-0.75 ? #C00000ff : #FF0000ff
plot( showCorrColor ? 0.0 : na, color=colorCorrelate, style=plot.style_circles, editable=false, linewidth=3, title="CorrColor")

//===== Zero Lines
plot( showCorrColor ? na : 0.0, color=#FFFF0022, linewidth=7, title="Zero"  , editable=false)
hline(                     0.0, color=showCorrColor ? color_none : #CCCCCCff, editable=false)