// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Shivam_Mandrai

//@version=6
strategy("IU BBB(Big Body Bar) Strategy", overlay = true, initial_capital = 100000, slippage = 1, commission_value = 0.002)

// _________________________________________________ User Inputs ______________________________________ \\
tooltip             = "Size of Body of the candle should be how much times the Average body ? "
big_body_threshold  = input.float(4, "Big Body Threshold = ", tooltip = tooltip)
ATR_lenght          = input.int(14,"ATR Lenght = ", group = "<------ Trailing Stop Loss -------- >")
ATR_factor          = input.float(2, "ATR factor = ",group = "<------ Trailing Stop Loss -------- >")

// _________________________________________________ Average body calculations _________________________ \\
body                = math.abs(close - open)
avg_body            = 0.00
for i = 0 to 20
    avg_body += body[i]

// ___________________________________________________ long and short Conditions _________________________ \\
avg_body           := avg_body / 20
long_cond           = body > (avg_body  * big_body_threshold) and close > open
short_cond          = body > (avg_body * big_body_threshold) and close < open


//___________________________________________ Going Long and Short ________________________________________ //\\
// -------- Long Entry ---------- \\
if long_cond and strategy.position_size == 0 and barstate.isconfirmed
    strategy.entry("long", strategy.long, comment = "Long Entry")
//-------- Short Entry --------- \\
if short_cond and strategy.position_size == 0 and barstate.isconfirmed
    strategy.entry("short", strategy.short, comment = "Short Entry")


// ____________________________________________ ATR Trailing Stop Loss ________________________________________ \\
float atr           = ta.atr(ATR_lenght)
var float ATR_TSL   = na
// -------- Long Side Trailing --------- \\
if strategy.position_size > 0
    // ---- Asigning trailing SL if its na ----- \\
    if na(ATR_TSL)
        ATR_TSL     := strategy.position_avg_price - (atr * ATR_factor)
    else
        ATR_TSL     := math.max(ATR_TSL, close - (atr * ATR_factor))
    // ---------- Setting Trailing SL --------------- \\
    strategy.exit("long", "long", stop = ATR_TSL, comment = "Trailing SL")

// -------- Short Side Trailing --------- \\
if strategy.position_size < 0
    // ---- Asigning trailing SL if its na ----- \\
    if na(ATR_TSL)
        ATR_TSL     := strategy.position_avg_price + (atr * ATR_factor)
    else
        ATR_TSL     := math.min(ATR_TSL, close + (atr * ATR_factor))
    // ---------- Setting Trailing SL --------------- \\
    strategy.exit("short", "short", stop = ATR_TSL, comment = "Short Trailing SL")

// ------ Resetting Trailing SL ------ \\
if strategy.position_size == 0
    ATR_TSL         := na


// ______________________________________________ Plotting _________________________________________________ \\
ATR_TSL_plot = plot(strategy.position_size != 0 ? ATR_TSL : na, color = color.purple,
                     style = plot.style_linebr, linewidth = 1)
mid_level    = plot(hl2, "Mid Level", display = display.none)
// ----------------- Filling Positions ---------------- \\
fill(ATR_TSL_plot, mid_level, hl2 ,ATR_TSL, color(na), strategy.position_size != 0 ? color.new(color.purple, 70) : na)