// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
//@version=6
strategy(title="Smart Money Pivot Strategy [Jason Kasei]", shorttitle="SM Pivot", overlay = true,margin_long=0, margin_short=0, default_qty_type=strategy.percent_of_equity,
 default_qty_value=100, process_orders_on_close=true, initial_capital=10000)

// Enable Long Strategy
enable_long_strategy = input.bool(true, title = 'Enable Long Strategy', inline = '1')
long_stoploss_value = input.float(defval = 1, title = 'Long SL %', minval = 0.1, inline = '2')
long_stoploss_percentage = close * (long_stoploss_value / 100) / syminfo.mintick
long_takeprofit_value = input.float(defval = 1.5, title = 'Long TP %', minval = 0.1, inline = '2')
long_takeprofit_percentage = close * (long_takeprofit_value / 100) / syminfo.mintick

// Enable Short Strategy
enable_short_strategy = input.bool(true, title = 'Enable Short Strategy', inline = '3')
short_stoploss_value = input.float(defval = 1, title = 'Short SL %', minval = 0.1, inline = '4')
short_stoploss_percentage = close * (short_stoploss_value / 100) / syminfo.mintick
short_takeprofit_value = input.float(defval = 1.5, title = 'Short TP %', minval = 0.1, inline = '4')
short_takeprofit_percentage = close * (short_takeprofit_value / 100) / syminfo.mintick

// Plot Stoploss & Take Profit Levels
long_stoploss_price = strategy.position_avg_price * (1 - long_stoploss_value / 100)
long_takeprofit_price = strategy.position_avg_price * (1 + long_takeprofit_value / 100)
short_stoploss_price = strategy.position_avg_price * (1 + short_stoploss_value / 100)
short_takeprofit_price = strategy.position_avg_price * (1 - short_takeprofit_value / 100)

var float UpdatedHigh = na
var float UpdatedLow = na
var int HighIndex = na
var bool phActive = false
var bool plActive = false
var int LowIndex = na
int Sync = bar_index
bool BUY = false
bool SELL = false

int period = input.int(20, 'Length', group = 'Smart Money Pivot')
// Color Definitions
BullBase = input.color(#00bcd4, title="Bull", inline="1", group = 'Smart Money Pivot')
BearBase = input.color(#ff9800, title="Bear", inline="1", group = 'Smart Money Pivot')

// Neon Effect Function (Adjusted for 2-argument color.new)
neon_bull = color.new(BullBase, 0)  // 半透明主色
neon_bull_glow = color.new(BullBase, 75)
neon_bear = color.new(BearBase, 0)
neon_bear_glow = color.new(BearBase, 75)

bool CandleType = 'Wicks' == 'Wicks'

PH = ta.pivothigh(high, period, period)
PL = ta.pivotlow(low, period, period)
ScrHigh = CandleType ? high : close
ScrLow = CandleType ? low : close

if ta.change(fixnan(PH)) != 0
    UpdatedHigh := PH
    phActive := true
    HighIndex := Sync - period

if ta.change(fixnan(PL)) != 0
    UpdatedLow := PL
    plActive := true
    LowIndex := Sync - period

// LONG
if ScrHigh > UpdatedHigh and phActive
    BUY := true
    phActive := false

// Sell
if ScrLow < UpdatedLow and plActive
    SELL := true
    plActive := false

LongSMB = BUY
ShortSMB = SELL

// Long Strategy
if LongSMB and enable_long_strategy and strategy.position_size <= 1
    strategy.entry('Long', strategy.long, alert_message = 'Long Entry')
    strategy.exit('Long', from_entry = 'Long', loss = long_stoploss_percentage, profit = long_takeprofit_percentage, alert_loss = 'Long SL', alert_profit = 'Long TP', comment_loss = 'Long SL', comment_profit = 'Long TP')

// Short Strategy
if ShortSMB and enable_short_strategy and strategy.position_size >= -1
    strategy.entry('Short', strategy.short, alert_message = 'Short Entry')
    strategy.exit('Short', from_entry = 'Short', loss = short_stoploss_percentage, profit = short_takeprofit_percentage, alert_loss = 'Short SL', alert_profit = 'Short TP', comment_loss = 'Short SL', comment_profit = 'Short TP')

// Visualization: Draw lines for pivot points with neon effect
style = line.style_solid

if BUY

    line.new(HighIndex, UpdatedHigh, bar_index, UpdatedHigh, color = neon_bull, style = line.style_solid, width = 1)
    line.new(HighIndex, UpdatedHigh + syminfo.mintick, bar_index, UpdatedHigh + syminfo.mintick, color = neon_bull_glow, style = line.style_solid, width = 5)
    line.new(HighIndex, UpdatedHigh - syminfo.mintick, bar_index, UpdatedHigh - syminfo.mintick, color = neon_bull_glow, style = line.style_solid, width = 5)
    label.new(
         x = HighIndex + (bar_index - HighIndex) / 2,  // x坐标居中
         y = UpdatedHigh + syminfo.mintick * 5,        // y坐标在主线条上方
         text = "BoS PH",
         color = color.new(BullBase, 75),              // 使用与glow相同的半透明颜色
         textcolor = BullBase,                         // 文字颜色使用主色
         style = label.style_none,                     // 无背景框
         size = size.tiny                              // 文字大小
         )

if SELL

    line.new(LowIndex, UpdatedLow, bar_index, UpdatedLow, color = neon_bear, style = line.style_solid, width = 1)
    line.new(LowIndex, UpdatedLow + syminfo.mintick, bar_index, UpdatedLow + syminfo.mintick, color = neon_bear_glow, style = line.style_solid, width = 5)
    line.new(LowIndex, UpdatedLow - syminfo.mintick, bar_index, UpdatedLow - syminfo.mintick, color = neon_bear_glow, style = line.style_solid, width = 5)
    label.new(
         x = LowIndex + (bar_index - LowIndex) / 2,    // x坐标居中
         y = UpdatedLow - syminfo.mintick * 5,         // y坐标在主线条下方
         text = "BoS PL",
         color = color.new(BearBase, 75),              // 使用与glow相同的半透明颜色
         textcolor = BearBase,                         // 文字颜色使用主色
         style = label.style_none,                     // 无背景框
         size = size.tiny                              // 文字大小
         )