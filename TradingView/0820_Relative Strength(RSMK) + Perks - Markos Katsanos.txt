//@version=4
study("Relative Strength(RSMK) + Perks - Markos Katsanos", "RMSK", false, format.price, 2)

bgcolor(color.new(#000000,15), title="Dark Background")

ema(Series, fPeriod) => // Overwrites Pine built-in ema() - Accepts a float for fPeriod
    alpha = 2.0 / (max(1.0, fPeriod) + 1.0)
    float return = na, return := alpha * Series + (1.0 - alpha) * nz(return[1], Series)
    return

rsmk(Asset, Index, iPeriod, fSmooth, iSignalPeriod) =>
    RSMK = ema(mom(log(Asset / Index), iPeriod), fSmooth) * 100.0
    signalRSMK = ema(RSMK, iSignalPeriod)
    [RSMK, signalRSMK]

mcrs(Asset, Index, iPeriod, fSmooth, iSignalPeriod) => // @midtownsk8rguy Comparative Relative Strength
    CRS = ema((-1.0 + (Asset * nz(Index[iPeriod])) / (nz(Asset[iPeriod]) * Index)), fSmooth) * 100.0
    signalCRS = ema(CRS, iSignalPeriod)
    [CRS, signalCRS]

relativeIndex = input("Manual Input",      "Comparative Market Index List", input.string , options=["Dow Jones","NASDAQ","RUSSEL 1000","RUSSEL 2000","RUSSEL 3000","Nifty","Manual Input"])
manualInput   = input(   "SPCFD:SPX",                 "      Manual Input", input.symbol ) // TRADINGVIEW MARKET INDICES LIST - https://www.tradingview.com/markets/indices/quotes-major/
source        = input(         close, "============= Source =============", input.source )
periodRSMK    = input(            90,                   " RSMK/CRS Period", input.integer,  minval=  5, step=5  )
smoothRSMK    = input(           3.0,                   " Smooth RSMK/CRS", input.float  ,  minval=1.0, step=0.5)
periodSignal  = input(            20,                     " Signal Period", input.integer,  minval= 10)
colorScheme   = input(    "Lime/Red", "========== Color Scheme ==========", input.string , options=["Lime/Red","Blue/Orange"])
showMCRS      = input(         "CRS", "Show Comparative Relative Strength", input.string , options=["Hidden","CRS","CRS + Signal"])
showCorrColor = input(         false,             "Show Correlation Color", input.bool   )
var SHOW_MCRS_SIGNAL = showMCRS=="CRS + Signal"
var SHOW_MCRS        = showMCRS=="CRS" or SHOW_MCRS_SIGNAL

//===== Comparative Index Selector
var COMPARE_TO = relativeIndex=="Dow Jones"   ? tickerid(    "DJ",   "DJI", session.regular) :
                 relativeIndex=="NASDAQ"      ? tickerid("NASDAQ",  "IXIC", session.regular) :
                 relativeIndex=="RUSSEL 1000" ? tickerid("RUSSEL",   "RUI", session.regular) :
                 relativeIndex=="RUSSEL 2000" ? tickerid("RUSSEL",   "RUT", session.regular) :
                 relativeIndex=="RUSSEL 3000" ? tickerid("RUSSEL",   "RUA", session.regular) :
                 relativeIndex=="Nifty"       ? tickerid(   "NSE", "NIFTY", session.regular) : manualInput
sMarketIndex = security(COMPARE_TO, timeframe.period, source)

//===== Markos Katsanos' Relative Strength (RSMK)
[RSMK, signalRSMK] = rsmk(source, sMarketIndex, periodRSMK, smoothRSMK, periodSignal)

colorRSMK = colorScheme=="Lime/Red" ?  RSMK>0.0 ? #00C000 : #C00000 :
                                       RSMK>0.0 ? #0099FF : #FF6600
colorSgnl = colorScheme=="Lime/Red" ? #FFCC00ff : #FF00C0ff
plot(      RSMK, color=colorRSMK,    transp=70, editable=false, style=plot.style_area     )
plot(      RSMK, color=colorRSMK,    transp= 0, editable=false, style=plot.style_histogram)
plot(      RSMK, color=colorRSMK,    transp= 0, editable=false, title="RSMK"  )
plot(signalRSMK, color=colorSgnl, linewidth= 2, editable=false, title="Signal")

//===== @midtownsk8rguy Comparative Relative Strength
[MCRS, signalCRS] = mcrs(source, sMarketIndex, periodRSMK, smoothRSMK, periodSignal)

colorMCRS =  MCRS>0.0 ? #00FF00ff : #FF0000ff
plot(SHOW_MCRS        ?      MCRS : na, color=colorMCRS, linewidth=2, editable=false, title="CRS")
plot(SHOW_MCRS_SIGNAL ? signalCRS : na, color=#0080FFff, linewidth=2, editable=false, title="CRS Signal")

//===== Correlation Color
correlate = correlation(source, sMarketIndex, periodRSMK)
colorCorrelate = correlate> 0.75 ? #00FF00ff :
                 correlate> 0.50 ? #00C000ff :
                 correlate> 0.25 ? #008000ff :
                 correlate> 0.0  ? #004000ff :
                 correlate>-0.25 ? #400000ff :
                 correlate>-0.50 ? #800000ff :
                 correlate>-0.75 ? #C00000ff : #FF0000ff
plot( showCorrColor ? 0.0 : na, color=colorCorrelate, style=plot.style_circles, editable=false, linewidth=2, title="CorrColor")

//===== Zero Lines
plot( showCorrColor ? na : 0.0, color=#FFFF0022, linewidth=7, title="Zero" , editable=false)
hline(                     0.0, color=showCorrColor ? color(na) : #FFFFFFff, editable=false)