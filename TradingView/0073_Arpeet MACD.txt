//@version=6
strategy('BNM INTRADAY SETUP MACD 3M - Version 1.2', shorttitle = 'Zero Lag MACD Enhanced 1.2')
source = close

fastLength = input.int(12, title = 'Fast MM period', minval = 1)
slowLength = input.int(26, title = 'Slow MM period', minval = 1)
signalLength = input.int(9, title = 'Signal MM period', minval = 1)
useEma = input(true, title = 'Use EMA (otherwise SMA)')
useOldAlgo = input(false, title = 'Use Glaz algo (otherwise \'real\' original zero lag)')
showDots = input(true, title = 'Show symbols to indicate crossing')
dotsDistance = input.float(1.5, title = 'Symbols distance factor', minval = 0.1)

// Fast line
ma1 = useEma ? ta.ema(source, fastLength) : ta.sma(source, fastLength)
ma2 = useEma ? ta.ema(ma1, fastLength) : ta.sma(ma1, fastLength)
zerolagEMA = 2 * ma1 - ma2

// Slow line
mas1 = useEma ? ta.ema(source, slowLength) : ta.sma(source, slowLength)
mas2 = useEma ? ta.ema(mas1, slowLength) : ta.sma(mas1, slowLength)
zerolagslowMA = 2 * mas1 - mas2

// MACD line
ZeroLagMACD = zerolagEMA - zerolagslowMA

// Signal line
emasig1 = ta.ema(ZeroLagMACD, signalLength)
emasig2 = ta.ema(emasig1, signalLength)
signal = useOldAlgo ? ta.sma(ZeroLagMACD, signalLength) : 2 * emasig1 - emasig2

hist = ZeroLagMACD - signal

upHist = hist > 0 ? hist : 0
downHist = hist <= 0 ? hist : 0

p1 = plot(upHist, color = color.new(color.blue, 40), style = plot.style_columns, title = 'Positive delta')
p2 = plot(downHist, color = color.new(color.red, 40), style = plot.style_columns, title = 'Negative delta')

zeroLine = plot(ZeroLagMACD, color = color.new(color.red, 0), linewidth = 2, title = 'MACD line')
signalLine = plot(signal, color = color.new(color.blue, 0), linewidth = 2, title = 'Signal')

ribbonDiff = hist > 0 ? color.blue : color.red
fill(zeroLine, signalLine, color = ribbonDiff)

circleYPosition = signal * dotsDistance
ribbonDiff2 = hist > 0 ? color.blue : color.red

// Generate dots for cross signals
plot(showDots and ta.cross(ZeroLagMACD, signal) ? circleYPosition : na, style = plot.style_circles, linewidth = 4, color = ribbonDiff2, title = 'Dots')

// Alerts for buy and sell signals
buySignal = ta.cross(ZeroLagMACD, signal) and ribbonDiff2 == color.blue and ZeroLagMACD < 0
sellSignal = ta.cross(ZeroLagMACD, signal) and ribbonDiff2 == color.red and ZeroLagMACD > 0

// Use 'strategy.entry' for placing orders in strategy context
if buySignal
    strategy.entry('Buy', strategy.long)
    alert('Buy Signal: Blue dot below zero line', alert.freq_once_per_bar_close)

if sellSignal
    strategy.entry('Sell', strategy.short)
    alert('Sell Signal: Red dot above zero line', alert.freq_once_per_bar_close)