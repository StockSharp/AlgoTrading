// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© rumpypumpydumpy

//@version=4
study("Time of day / day of week Sigma Spike", overlay=false, max_lines_count = 24, max_labels_count = 24)
thresh = input(2.50, title="Sigma Spike threshold")
all_days = input(false, title="[TRUE] : All days of the week - [FALSE] : select one day only")
inp_dow = input("SUN", options=["SUN", "MON", "TUE", "WED", "THU", "FRI", "SAT"], title= "Day of week")

dow = inp_dow == "SUN" ? dayofweek.sunday : inp_dow == "MON" ? dayofweek.monday : inp_dow == "TUE" ? dayofweek.tuesday : inp_dow == "WED" ? dayofweek.wednesday : inp_dow == "THU" ? dayofweek.thursday : inp_dow == "FRI" ? dayofweek.friday : dayofweek.saturday

f_sigma_spike(_close, _len)=>
    _ret = _close / _close[1] - 1
    _sdret = stdev(_ret, _len)
    _ret / _sdret[1]

H1_SS = abs(f_sigma_spike(close, 20))

var int[] H_SS = array.new_int(24,0)
var int[] xtime = array.new_int(24,0)

for i = 0 to 23
    array.set(xtime, i, time[i])

if barstate.isconfirmed
    if all_days and H1_SS >= thresh
        array.set(H_SS, hour, array.get(H_SS, hour) + 1)
    else if not all_days and H1_SS >= thresh and dayofweek == dow
        array.set(H_SS, hour, array.get(H_SS, hour) + 1)

for i = 0 to 23
    l = line.new(x1 = array.get(xtime, 23 - i),
                 y1 = 0,
                 x2 = array.get(xtime, 23 - i),
                 y2 = array.get(H_SS, i) / array.sum(H_SS) * 100,
                 xloc = xloc.bar_time,
                 width = 3,
                 color = color.white)
    line.delete(l[1])
    lab = label.new(x = array.get(xtime, 23 - i),
                     y = -0.5,
                     xloc = xloc.bar_time,
                     text = tostring(i),
                     color = color.white,
                     textcolor = color.black,
                     style = label.style_label_up,
                     size = size.tiny)
    label.delete(lab[1])

hline(0)