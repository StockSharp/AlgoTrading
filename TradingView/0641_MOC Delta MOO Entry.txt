//@version=6
strategy("MOC Delta MOO Entry", overlay=true)

// === INPUTS ===
showLabels       = input.bool(true, "Show Volume Labels")
extendLines      = input.bool(true, "Extend Lines Right?")
lineLengthInput  = input.int(50, "Line Extension Length (max 100)", minval=1, maxval=10000)
lineWidth        = input.int(1, "Line Thickness", minval=1, maxval=5)
lineLength = math.min(lineLengthInput, 10000)
extend     = extendLines ? lineLength : 0

// === RISK MANAGEMENT ===
tickSize = syminfo.mintick
tpTicks = input.int(20, "Take Profit (Ticks)", minval=1)
slTicks = input.int(10, "Stop Loss (Ticks)", minval=1)


afternoonColor   = input.color(color.red, "Afternoon Range Color")

inAfternoonRange = (hour == 14 and minute >= 50) or (hour == 14 and minute == 55)
endAfternoonBar  = (hour == 14 and minute == 55)

// === DAILY VOLUME TRACKING (18:00 to 16:59) ===
sessionReset = (hour == 17 and minute == 0)
var float dailyVolume = 0.0

if sessionReset
    dailyVolume := 0

dailyVolume += volume

// === AFTERNOON SESSION TRACKING ===
var float afternoonHigh = na
var float afternoonLow = na
var float afternoonVol = na
var float afternoonBuyVol = na
var float afternoonSellVol = na
var bool afternoonTracking = false

deltaThreshold = input.float(2.0, "Delta % Threshold", minval=0.1, step=0.1)

if inAfternoonRange
    afternoonHigh := afternoonTracking ? math.max(afternoonHigh, high) : high
    afternoonLow  := afternoonTracking ? math.min(afternoonLow, low)   : low
    afternoonVol  := afternoonTracking ? afternoonVol + volume         : volume
    afternoonBuyVol := afternoonTracking ? afternoonBuyVol + (close > open ? volume : 0) : (close > open ? volume : 0)
    afternoonSellVol := afternoonTracking ? afternoonSellVol + (close < open ? volume : 0) : (close < open ? volume : 0)
    afternoonTracking := true

// === MOC Delta Logic ===
var float mocDelta = na
var float mocDeltaPct = na

if endAfternoonBar
    mocDelta := afternoonBuyVol - afternoonSellVol
    mocDeltaPct := dailyVolume > 0 ? (mocDelta / dailyVolume) * 100 : na

if endAfternoonBar
    line.new(bar_index, afternoonHigh, bar_index + extend, afternoonHigh, color=afternoonColor, width=lineWidth)
    line.new(bar_index, afternoonLow, bar_index + extend, afternoonLow, color=afternoonColor, width=lineWidth)
    if showLabels
        label.new(bar_index, afternoonHigh, "Δ Buy-Sell: " + str.tostring(mocDelta, format.volume) + " | Δ %: " + (dailyVolume > 0 ? str.tostring((mocDelta / dailyVolume) * 100, "#.##") + "%" : "n/a") + " | Daily Vol: " + str.tostring(dailyVolume, format.volume), xloc.bar_index, yloc.price, color=afternoonColor, style=label.style_label_down, textcolor=color.white, size=size.small)
    afternoonHigh := na
    afternoonLow := na
    afternoonVol := na
    afternoonBuyVol := na
    afternoonSellVol := na
    afternoonTracking := false

// === MOC Delta-Based Signals ===
is830am = (hour == 8 and minute == 30)
bullishMOC = mocDeltaPct > deltaThreshold
bearishMOC = mocDeltaPct < -deltaThreshold

sma15 = ta.sma(open, 15)
sma30 = ta.sma(open, 30)

validLong = open > sma15 and open > sma30
validShort = open < sma15 and open < sma30

longSignal = is830am and bullishMOC and validLong
shortSignal = is830am and bearishMOC and validShort

// === Visual Plots ===
plotshape(longSignal, location=location.belowbar, color=color.green, style=shape.triangleup, size=size.small, title="Long Entry")
plotshape(shortSignal, location=location.abovebar, color=color.red, style=shape.triangledown, size=size.small, title="Short Entry")
plot(sma15, color=color.orange, title="SMA 15")
plot(sma30, color=color.blue, title="SMA 30")

if (longSignal)
    strategy.entry("Long MOC", strategy.long)
    strategy.exit("TP/SL Long", from_entry="Long MOC", limit=close + tickSize * tpTicks, stop=close - tickSize * slTicks)

if (shortSignal)
    strategy.entry("Short MOC", strategy.short)
    strategy.exit("TP/SL Short", from_entry="Short MOC", limit=close - tickSize * tpTicks, stop=close + tickSize * slTicks)

closeTime = (hour == 14 and minute == 50)
if closeTime
    strategy.close_all(comment="15:50 Close")