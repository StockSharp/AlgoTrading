// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© LonesomeTheBlue

//@version=4
study("Multi Time Frame Candles")
Timeframe = input(defval = 'D', title = "Time Frame", type = input.resolution)
numofcandles = input(defval = 8, title = "Number of Candles", minval = 1, maxval = 10)
colorup = input(defval = color.lime, title = "Color Up", type = input.color)
colordown = input(defval = color.red, title = "Color Down", type = input.color)
colorwick = input(defval = color.gray, title = "Wick Color", type = input.color)
candlewidth = input(defval = 30, title = "Candle Width", minval = 1)

bool newbar = change(time(Timeframe)) != 0
var float [] candles = array.new_float(40, na) // ohlc * 10
if newbar
    for x = 1 to 4
        array.pop(candles)
    array.unshift(candles, close)
    array.unshift(candles, low)
    array.unshift(candles, high)
    array.unshift(candles, open)
else if array.size(candles) > 3
    array.set(candles, 1, max(array.get(candles, 1), high))
    array.set(candles, 2, min(array.get(candles, 2), low))
    array.set(candles, 3, close)

plot_candle(ind, loc)=>
    var line body = na, var line top = na, var line bottom = na
    line.delete(body[1]), line.delete(top[1]), line.delete(bottom[1])
    addition = array.get(candles, ind + 0) == array.get(candles, ind + 3) ? syminfo.mintick / 5 : 0
    body := line.new(x1 = loc, y1 = array.get(candles, ind + 0), x2 = loc, y2 = array.get(candles, ind + 3) + addition, xloc = xloc.bar_time, color = array.get(candles, ind + 3) > array.get(candles, ind + 0) ? colorup : colordown, width = candlewidth)
    top := line.new(x1 = loc, y1 = max(array.get(candles, ind + 0), array.get(candles, ind + 3)), x2 = loc, y2 = array.get(candles, ind + 1), xloc = xloc.bar_time, color = colorwick, width = 2)
    bottom := line.new(x1 = loc, y1 = min(array.get(candles, ind + 0), array.get(candles, ind + 3)), x2 = loc, y2 = array.get(candles, ind + 2), xloc = xloc.bar_time, color = colorwick, width = 2)

plot_candle(0, time + 0)
if numofcandles > 1
    plot_candle(4, time[6])
    if numofcandles > 2
        plot_candle(8, time[12])
        if numofcandles > 3
            plot_candle(12, time[18])
            if numofcandles > 4
                plot_candle(16, time[24])
                if numofcandles > 5
                    plot_candle(20, time[30])
                    if numofcandles > 6
                        plot_candle(24, time[36])
                        if numofcandles > 7
                            plot_candle(28, time[42])
                            if numofcandles > 8
                                plot_candle(32, time[48])
                                if numofcandles > 9
                                    plot_candle(36, time[54])