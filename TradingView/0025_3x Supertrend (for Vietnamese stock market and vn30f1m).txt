// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © doqkhanh

//@version=6
strategy('4Vietnamese 3x Supertrend', overlay=true, max_bars_back=1000, initial_capital = 10000000000, slippage = 2, commission_type = strategy.commission.percent, commission_value = 0.013, default_qty_type=strategy.percent_of_equity, default_qty_value = 33, pyramiding = 3, margin_long = 0, margin_short = 0)

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Inputs

// Supertrend Settings
STATRLENGTH1 = input.int(11, title='Fast Supertrend ATR Length', group='SUPERTREND SETTINGS')
STATRMULT1 = input.float(1, title='Fast Supertrend ATR Multiplier', group='SUPERTREND SETTINGS')
STATRLENGTH2 = input.int(12, title='Medium Supertrend ATR Length', group='SUPERTREND SETTINGS')
STATRMULT2 = input.float(2, title='Medium Supertrend ATR Multiplier', group='SUPERTREND SETTINGS')
STATRLENGTH3 = input.int(13, title='Slow Supertrend ATR Length', group='SUPERTREND SETTINGS')
STATRMULT3 = input.float(3, title='Slow Supertrend ATR Multiplier', group='SUPERTREND SETTINGS')

isUseHighestOf2RedCandleSetup = input.bool(false, group = "Setup Filters")


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Calculations
[superTrend1, dir1] = ta.supertrend(STATRMULT1, STATRLENGTH1)
[superTrend2, dir2] = ta.supertrend(STATRMULT2, STATRLENGTH2)
[superTrend3, dir3] = ta.supertrend(STATRMULT3, STATRLENGTH3)


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Calculate highest from supertrend1 uptrend
var float highestGreen = 0
if dir1 < 0 and highestGreen == 0 and (isUseHighestOf2RedCandleSetup ? close < open : true)
    highestGreen := high
if highestGreen > 0 and (isUseHighestOf2RedCandleSetup ? close < open : true)
    if high > highestGreen
        highestGreen := high
if dir1 >= 0
    highestGreen := 0


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Add trade window to start find entry
tradeStartWindow = input.time(0, confirm=true, title="Trade Start Window", group="Trade Window")
isInTradeWindow = time >= tradeStartWindow


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Entry SL
var entrySL4Long1 = false
var entrySL4Long2 = false
var entrySL4Long3 = false

isUseEntrySL = input.bool(false, group = "Entry SL Option")
dataToCalculate = input.source(low, group = "Entry SL Option")

if isUseEntrySL and (dir1 > 0 and dir2 < 0 and dir3 < 0)
    if strategy.opentrades >= 1
        if dataToCalculate > strategy.opentrades.entry_price(0)
            entrySL4Long1 := true
        else
            entrySL4Long1 := false

        if entrySL4Long1 and close > strategy.opentrades.entry_price(0)
            strategy.exit('exit1', from_entry = strategy.opentrades.entry_id(0), stop = strategy.opentrades.entry_price(0))

    if strategy.opentrades >= 2
        if dataToCalculate > strategy.opentrades.entry_price(1)
            entrySL4Long2 := true
        else
            entrySL4Long2 := false

        if entrySL4Long2 and close > strategy.opentrades.entry_price(1)
            strategy.exit('exit2', from_entry = strategy.opentrades.entry_id(1), stop = strategy.opentrades.entry_price(1))

    if strategy.opentrades >= 3
        if dataToCalculate > strategy.opentrades.entry_price(2)
            entrySL4Long3 := true
        else
            entrySL4Long3 := false

        if entrySL4Long3 and close >  strategy.opentrades.entry_price(2)
            strategy.exit('exit3', from_entry = strategy.opentrades.entry_id(2), stop = strategy.opentrades.entry_price(2))

// Control entry sl condition
if strategy.closedtrades > strategy.closedtrades[1]
    if strategy.closedtrades.exit_id(strategy.closedtrades-1) == 'exit3'
        entrySL4Long3 := false
    if strategy.closedtrades.exit_id(strategy.closedtrades-1) == 'exit2'
        entrySL4Long2 := false
    if strategy.closedtrades.exit_id(strategy.closedtrades-1) == 'exit1'
        entrySL4Long1 := false


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Entry
isUseEntryWithHigherPrice = input.bool(true, title="Only entry if current close higher than previous close", group = "Entry Type")
if isInTradeWindow
    if strategy.opentrades == 0
         or ((isUseEntryWithHigherPrice and strategy.opentrades > 0) ? close > strategy.opentrades.entry_price(strategy.opentrades-1) : true)
        if dir3 < 0
            if dir2 > 0 and dir1 < 0
                strategy.entry('long' + str.tostring(strategy.opentrades + 1), strategy.long)
            else if dir2 < 0
                strategy.entry('long' + str.tostring(strategy.opentrades + 1), strategy.long, stop=superTrend1)
        else
            if dir1 < 0 and highestGreen > 0
                strategy.entry('long' + str.tostring(strategy.opentrades + 1), strategy.long, stop=highestGreen)


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Exit
isUseOnlyCloseInLossPositionSubmitEntrySLForPositionInProfit = input.bool(false, group = "Exit Only In Loss To Build Long Term Positions")

//---------------------------------------------------------------------------------------------------------
isUseAllDowntrendExit = input.bool(true, title="Only close_all if we had all 3 downtrend-supertrend ", group = "Exit Type")
if isUseAllDowntrendExit
     and dir3 > 0 and dir2 > 0 and dir1 > 0 // all supertrend is downtrend
     and ((close < open) or (close >= open and open < close[1])) // we had a red candle or we had a gap down

    //close if open position in loss, create exit order stop at entry price for all position not in loss (in profit with current close)
    if isUseOnlyCloseInLossPositionSubmitEntrySLForPositionInProfit
        if strategy.opentrades > 0
            for i = 0 to strategy.opentrades - 1
                if strategy.opentrades.entry_price(i) > close
                    strategy.close(id=strategy.opentrades.entry_id(i))
                else
                    strategy.exit('exit' + strategy.opentrades.entry_id(i), from_entry=strategy.opentrades.entry_id(i), stop=strategy.opentrades.entry_price(i))
    else
        strategy.close_all()
else //cancel all exit order if we have sign of back to uptrend
    if not (dir3 > 0 and dir2 > 0 and dir1 > 0)
        if strategy.opentrades > 0
            for i = 0 to strategy.opentrades - 1
                strategy.cancel(id='exit' + strategy.opentrades.entry_id(i))

//---------------------------------------------------------------------------------------------------------
isUseAvgPriceInLoss = input.bool(true, title="Only close_all if position_avg_price under current close (in loss)", group = "Exit Type")
if isUseAvgPriceInLoss and strategy.position_avg_price > close

    if isUseOnlyCloseInLossPositionSubmitEntrySLForPositionInProfit
        if strategy.opentrades > 0
            for i = 0 to strategy.opentrades - 1
                if strategy.opentrades.entry_price(i) > close
                    strategy.close(id=strategy.opentrades.entry_id(i))
                else
                    strategy.exit('exit' + strategy.opentrades.entry_id(i), from_entry=strategy.opentrades.entry_id(i), stop=strategy.opentrades.entry_price(i))
    else
        strategy.close_all()
else
    if not (strategy.position_avg_price > close)
        if strategy.opentrades > 0
            for i = 0 to strategy.opentrades - 1
                strategy.cancel(id='exit' + strategy.opentrades.entry_id(i))

//---------------------------------------------------------------------------------------------------------
isUseAllPositionsInLoss = input.bool(false, title="If all positions in loss, close them all", group = "Exit Type")

if isUseAllPositionsInLoss
    positionInLostCount = 0
    for i = 0 to strategy.opentrades - 1
        if strategy.opentrades.entry_price(i) > close
            positionInLostCount += 1

    if positionInLostCount == strategy.opentrades
        strategy.close_all('Close all positions in loss')

//---------------------------------------------------------------------------------------------------------
// isThereIsOnePositionInLoss = false
// isUseClosePositionInLostInmediately = input.bool(false, title="Only close position in loss under 3 downtrend add entry sl for the rest", group = "Exit Type")
// if isUseClosePositionInLostInmediately and (dir3 > 0 and dir2 > 0 and dir1 > 0)
//     if strategy.opentrades > 0
//         for i = 0 to strategy.opentrades - 1
//             if strategy.opentrades.entry_price(i) > close
//                 strategy.close(id=strategy.opentrades.entry_id(i))
//                 isThereIsOnePositionInLoss := true
//             else
//                 strategy.exit('exit' + strategy.opentrades.entry_id(i), from_entry=strategy.opentrades.entry_id(i), stop=strategy.opentrades.entry_price(i))
// else
//     if strategy.opentrades > 0 and (not isThereIsOnePositionInLoss)
//         for i = 0 to strategy.opentrades - 1
//             strategy.cancel(id='exit' + strategy.opentrades.entry_id(i))

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Debug sell if we are at the last candle
isUseDebugSell = input.bool(false, group="Debug")
if isUseDebugSell
    if bar_index == last_bar_index-1
        strategy.close_all('Debug Sell')

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Plot
plot(superTrend1, title='Fast Supertrend',      color=dir1 == 1 and dir1[1] == 1 ? color.red : dir1 == -1 and dir1[1] == -1 ? color.green : na)
plot(superTrend2, title='Medium Supertrend',    color=dir2 == 1 and dir2[1] == 1 ? color.red : dir2 == -1 and dir2[1] == -1 ? color.green : na)
plot(superTrend3, title='Slow Supertrend',      color=dir3 == 1 and dir3[1] == 1 ? color.red : dir3 == -1 and dir3[1] == -1 ? color.green : na)