import TradingView/Strategy/3
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© shivammandrai

//@version=6
strategy('IU EMA Channel Strategy', overlay = true, initial_capital = 100000, slippage = 1, commission_value = 0.02)
// ____________________________________________ User input __________________________________________ \\
RTR = input.float(2, "RTR = ", tooltip = "Risk to Reward ")
ema_lenght = input.int(100 , 'EMA Lenght = ')

// ____________________________________________ Indiactors ___________________________________________ \\
high_based_ema = ta.ema(high, ema_lenght)
low_based_ema = ta.ema(low, ema_lenght)

//@function -> Returns color green if value1 > value2 else color red with given transp
set_color(value1, value2, trans = 60)=>
    color_type = color(na)
    if value1 > value2
        color_type := color.new(color.green, trans)
    if value1 < value2
        color_type := color.new(color.red, trans)
    color_type

// _________________________________________ Plotting the EMAs __________________________________________ \\
high_based_ema_plot = plot(high_based_ema, "High Based EMA", set_color(close , high_based_ema), 2)
low_based_ema_plot  = plot(low_based_ema , "Low Based EMA" , set_color(close , high_based_ema), 2)
// ---------- Filling the EMA ---------- \\
fill(high_based_ema_plot, low_based_ema_plot, top_value = high_based_ema,
         bottom_value = low_based_ema, top_color = color(na), bottom_color = set_color(close , high_based_ema, 20))

// ____________________________________ Long and Short Condtion ___________________________________________ \\
high_cross = ta.crossover(close, high_based_ema)
low_cross  = ta.crossunder(close, low_based_ema)
// ----------- Going long ------------ \\
if high_cross and strategy.position_size == 0 and barstate.isconfirmed
    strategy.entry("long", strategy.long, comment = "Long Entry")
// ----------- Going Short ------------ \\
if low_cross and strategy.position_size == 0 and barstate.isconfirmed
    strategy.entry("short", strategy.short, comment = "Short Entry")

//_____________________________ Stop Loss and Take Profit ____________________________\\
// ---- long and short SL ------ \\
long_SL = ta.valuewhen(strategy.position_size > 0 and strategy.position_size[1] == 0 , low[1], 0)
short_SL = ta.valuewhen(strategy.position_size < 0 and strategy.position_size[1] == 0, high[1], 0)
// ----- long and short TP ------ \\
long_TP = (strategy.position_avg_price - long_SL) * RTR + strategy.position_avg_price
short_TP = strategy.position_avg_price - (short_SL - strategy.position_avg_price) * RTR

// ____________________________ Stting long and short SL / TP ___________________________\\
if strategy.position_size > 0
    strategy.exit("long", "long", stop = long_SL, limit = long_TP, comment = "Long Exit")

if strategy.position_size < 0
    strategy.exit("short", "short", stop = short_SL, limit = short_TP, comment = "Short Exit")

//______________________________ Plotting long and short SL , TP ___________________________________\\
// ---- for long positions ------ \\
long_SL_plot = plot(strategy.position_size[1] > 0 ? long_SL : na   , 'Long SL', color.red  , style = plot.style_linebr)
long_TP_plot = plot(strategy.position_size[1] > 0 ? long_TP[1] : na, 'Long TP', color.green, style = plot.style_linebr)
// ---- for short positions ----- \\
short_SL_plot = plot(strategy.position_size[1] < 0 ? short_SL : na   , 'Short SL', color.red  , style = plot.style_linebr)
short_TP_plot = plot(strategy.position_size[1] < 0 ? short_TP[1] : na, 'Short SL', color.green, style = plot.style_linebr)

//______________________________ Filling stop loss and take profits ______________//
entry_plot = plot(strategy.position_size[1] != 0 ? strategy.position_avg_price[1] : na,
             color = color.new(color.silver, 20), style = plot.style_linebr)
// ------------- for long ----------- \\
fill(entry_plot, long_SL_plot , color =color.new(color.red, 85)   , title = 'Long SL plot')
fill(entry_plot, long_TP_plot , color = color.new(color.green, 85), title = 'Long TP plot')
// ------------- for short ---------- \\
fill(entry_plot, short_SL_plot, color.new(color.red, 85)  , 'Short SL plot')
fill(entry_plot, short_TP_plot, color.new(color.green, 85), 'Short TP plot')