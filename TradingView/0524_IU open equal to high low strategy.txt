// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/


//@version=6
strategy('IU open equal to high/low strategy', overlay = true, initial_capital = 100000, slippage = 1, commission_value = 0.02)

// _____________________________________________ User input ___________________________________________________\\
RTR = input.float(2.00, 'RTR = ', tooltip = "Risk to Reward Ratio")

// _____________________________________________ getting the open, high and low _______________________________\\
days_first_open = ta.valuewhen(session.isfirstbar, open, 0)
days_first_high = ta.valuewhen(session.isfirstbar, high, 0)
days_first_low  = ta.valuewhen(session.isfirstbar, low , 0)


//_______________________________________________ long short cond ______________________________________________\\
long_cond  = days_first_open == days_first_low  and session.isfirstbar
short_cond = days_first_open == days_first_high and session.isfirstbar

// -------- going long ----------\\
if long_cond and strategy.position_size == 0
    strategy.entry('long', strategy.long, comment = 'Long Entry')
    // -------- going short ----------\\
if short_cond and strategy.position_size == 0 and session.isfirstbar
    strategy.entry('short', strategy.short, comment = 'Short Entry')


//____________________________________ Long and short exits __________________________________________\\
long_SL  = ta.valuewhen(strategy.position_size > 0 and strategy.position_size[1] == 0 , low[1], 0)
long_TP  = strategy.position_avg_price + (strategy.position_avg_price - long_SL) * RTR
short_SL = ta.valuewhen(strategy.position_size < 0 and strategy.position_size[1] == 0 , high[1], 0)
short_TP = strategy.position_avg_price - (short_SL - strategy.position_avg_price) * RTR

//--------- long exit ---------\\
if strategy.position_size > 0
    strategy.exit('long', 'long', stop = long_SL, limit = long_TP, comment = 'Long Exit')
    //--------- short exit ---------\\
if strategy.position_size < 0
    strategy.exit('short', 'short', stop = short_SL, limit = short_TP, comment = 'Short Exit')


// ________________________________________ plotting long and short SL/TP ______________________________\\
//-------- long and short SL/TP ----------\\
long_SL_plot  = plot(strategy.position_size[1]  > 0 ? long_SL     : na, 'Long SL' , color.red  , style = plot.style_linebr)
long_TP_plot  = plot(strategy.position_size[1]  > 0 ? long_TP[1]  : na, 'Long TP' , color.green, style = plot.style_linebr)
short_SL_plot = plot(strategy.position_size[1] < 0 ? short_SL     : na, 'Shprt SL', color.red  , style = plot.style_linebr)
short_TP_plot = plot(strategy.position_size[1] < 0 ? short_TP[1]  : na, 'Short TP', color.green, style = plot.style_linebr)
// -------- plotting labels --------------\\
plotshape(long_cond , 'Open==Low' , shape.labelup  , location.belowbar, color.new(color.green,80), size = size.huge
             , text = "Open == Low" , textcolor = color.white)
plotshape(short_cond, 'Open==High', shape.labeldown, location.abovebar, color.new(color.red,80)  , size = size.huge
             , text = "Open == high", textcolor = color.white)



//______________________________ Filling stop loss and take profits ______________//
entry_plot = plot(strategy.position_size[1] != 0 ? strategy.position_avg_price[1] : na, color = color.new(color.silver, 20)
             , style = plot.style_linebr)
// ------------- for long ----------- \\
fill(entry_plot, long_SL_plot , color.new(color.red, 85)  , 'Long SL plot')
fill(entry_plot, long_TP_plot , color.new(color.green, 85), 'Long TP plot')
// ------------- for short ---------- \\
fill(entry_plot, short_SL_plot, color.new(color.red, 85)  , 'Short SL plot')
fill(entry_plot, short_TP_plot, color.new(color.green, 85), 'Short TP plot')