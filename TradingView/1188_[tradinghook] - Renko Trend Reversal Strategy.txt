//@version=5
strategy(title='[tradinghook] - Renko Trend Reversal Strategy', shorttitle='[tradinghook] - Renko TRS', overlay=true, initial_capital = 1000, commission_value = 0.05, default_qty_value = 2    )

// INPUTS
renkoATRLength = input.int(10, minval=1, title='ATR Length')
startDate = input.time(timestamp("01 Jan 2021 00:00"), title="Start Date")
endDate = input.time(timestamp("31 Dec 2025 23:59"), title="End Date")

enableSignals = input.bool(true, title="Enable Buy/Sell Signals")
enableSlTp = input.bool(true, title="Enable Stop Loss / Take Profit")
stopLossPct = input.float(10, title='Stop Loss Percentage', step=1)
takeProfitPct = input.float(50, title='Take Profit Percentage', step=1)


// CALCULATION
param = ticker.renko(syminfo.tickerid, 'ATR', renkoATRLength)
var float stopLossPrice = na
var float takeProfitPrice = na

renkoClose = request.security(param, timeframe.period, close)
renkoOpen = request.security(param, timeframe.period, open)

plot(renkoOpen, title="Renko Open", style=plot.style_line, linewidth=2, color=renkoClose < renkoOpen ?  color.red : color.green )

if(enableSignals)
    isWithinTimeRange = time >= startDate and time <= endDate
    buySignal = ta.crossunder(renkoOpen, renkoClose) and isWithinTimeRange
    sellSignal = ta.crossover(renkoOpen, renkoClose) and isWithinTimeRange

    if (buySignal)
        strategy.entry("Long", strategy.long)
        if (enableSlTp)
            stopLossPrice := renkoOpen * (1 - stopLossPct / 100)
            takeProfitPrice := renkoOpen * (1 + takeProfitPct / 100)
            strategy.exit("ExitLong", "Long", stop = stopLossPrice, limit = takeProfitPrice, comment="SL: " + str.tostring(stopLossPrice) + ", TP: " + str.tostring(takeProfitPrice))
    if (sellSignal)
        strategy.entry("Short", strategy.short)
        if (enableSlTp)
            stopLossPrice := renkoOpen * (1 + stopLossPct / 100)
            takeProfitPrice := renkoOpen * (1 - takeProfitPct / 100)
            strategy.exit("ExitShort", "Short", stop = stopLossPrice, limit = takeProfitPrice, comment="SL: " + str.tostring(stopLossPrice) + ", TP: " + str.tostring(takeProfitPrice))